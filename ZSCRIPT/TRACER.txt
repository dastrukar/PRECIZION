//aka projectiless
class bullettracer : FastProjectile
{
Default
  {
  Projectile;
  +NOTIMEFREEZE;
  +NOBLOCKMAP;
  MissileType "bt";
  MissileHeight 9;
  Speed 300;
  Damage 0;
  Scale 1;
  Radius 1;
  Height 1;
  }
	States
	{
		Spawn:
			TNT1 A 1;
			loop;
		Death:
			TNT1 A 1;
			stop;
	}
}
class bullettracerl : bullettracer
{
Default
  {
  MissileType "btl";
  }
}

class bt : Actor //used for bullettracer's "missiletype"
{
Default
  {
  RenderStyle "Add";
  Alpha 0.95;
  Scale 0.50;
  +NOCLIP;
  +FORCEXYBILLBOARD
  +NOBLOCKMAP;
  -NOTIMEFREEZE;
  +NOTELEPORT;
  +NOGRAVITY
  }
	States
	{
		Spawn:
			PUFF A 1 A_FadeOut(0.20, FTF_REMOVE);
			loop;
	}
}
class btl : bt //a larger version of bt
{
Default
  {
  Scale 1.25;
  }
  
	States
	{
		Spawn:
			BBLT A 1 A_FadeOut(0.20, FTF_REMOVE);
			loop;
	}
}

class ebullet : bullettracer
{
Default
  {
  -NOTIMEFREEZE;
  MissileType "ebt";
  Speed 50;
  DamageFunction random(1,5)*3;
  }
}
class ebt : bt
{
}

class bnt : bt
{
Default
  {
  +FORCEXYBILLBOARD;
  Alpha 0.5;
  Scale 0.25;
  }
}


class bulletsmok : BulletPuff
{
  
	States
	{
		Spawn:
			TNT1 AAAAAAAAAAAA 0 A_SpawnDebris("bspark");
			PUFF A 4 Bright;
			PUFF BCD 4;
			stop;
		Melee:
			PUFF CD 4;
			Stop;
	}
}

class bspark : Actor
{
Default
  {
  Projectile;
  Health 1;
  RenderStyle "Add";
  Alpha 0.5;
  Scale 0.25;
  Gravity 0.5;
  BounceType "Doom";
  BounceCount 2;
  -NOGRAVITY;
  +FORCEXYBILLBOARD;
  }
  
	States
	{
		Spawn:
			TNT1 A 0;
		RJump:
			TNT1 A 0 A_Jump(256, 1, 2, 3, 4);
			SPAK ABCD 0 A_Jump(256, "s");
		s:
			"----" A -1;
			stop;
	}
}


//===============================================================
// BFG Projectile stuff
//-------------------------------------------
class bfgballs : FastProjectile
{
Default
  {
  Projectile;
  Speed 50;
  Scale 0.25;
  Damage 50;
  RenderStyle "Add";
  Alpha 0.75;
  MissileType "bfgtrail";
  MissileHeight 9;
  DeathSound "weapons/bfgx";
  Obituary "$OB_MPBFG_BOOM";
  +RANDOMIZE;
  }
  
	States
	{
		Spawn:
			BFS1 AB 4 Bright A_SpawnItemEx("bfgsspawn");
			loop;
		Death:
			BFE1 AB 4 Bright;
			BFE1 C 4 Bright A_BFGSpray("bfgtracer", 20, 30, 360, 16*64, 32, 0, BFGF_MISSILEORIGIN);
			BFE1 DEF 4 Bright;
			stop;
	}
}
class bfgtracer : Actor //tracer damage tied to the bfgshot
{
Default
  {
  +NOBLOCKMAP;
  +NOGRAVITY;
  RenderStyle "Add";
  Alpha 0.75;
  Scale 0.50;
  DamageType "BFGSplash";
  }
 
	States
	{
		Spawn:
			BFE2 ABCD 8 Bright;
			stop;
	}
}

class bfgtrail : Actor //used for the bfgshot missiletype
{
Default
  {
  Projectile;
  RenderStyle "Add";
  Alpha 0.65;
  Scale 0.25;
  Speed 0;
  +NOCLIP;
  +FORCEXYBILLBOARD;
  +NOBLOCKMAP;
  -NOTIMEFREEZE;
  +NOTELEPORT;
  +NOGRAVITY;
  }
  
	States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 0 A_Jump(256, "a1", "a2");
		a1:
			BFS1 A 0;
			goto Freeze;
		a2:
			BFS1 B 0;
			goto Freeze;
		Freeze:
			"####" "#" 2 A_FadeOut(0.20, FTF_REMOVE);
			loop;
	}
}

class bfgsspawn : Actor //spawns sparks
{
Default
  {
  +NOCLIP;
  +FORCEXYBILLBOARD;
  +NOBLOCKMAP;
  -NOTIMEFREEZE;
  +NOTELEPORT;
  +NOGRAVITY;
  }
  
  States
	{
		Spawn:
			TNT1 A 0;
			TNT1 AAAAAA 0 A_SpawnItemEx("bfgspark", frandom(-12, 12), frandom(-12, 12), frandom(-12, 12));
			stop;
	}
}
class bfgspark : bfgtrail //bfg sparks
{
Default
  {
  Scale 1;
  }
  
  States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 0 A_Jump(256, "a1", "a2");
		a1:
			BSPK A 0;
			goto Freeze;
		a2:
			BSPK B 0;
			goto Freeze;
			
		Freeze:
			"####" "#" 6 A_FadeOut(0.10, FTF_REMOVE);
			loop;
	}
}


//===============================================================
// Rocket Projectile stuff
//-------------------------------------------
class RPGocket : FastProjectile
{
Default
  {
  Radius 11;
  Height 8;
  Speed 30;
  Damage 20;
  Scale 0.75;
  Projectile;
  +RANDOMIZE;
  +DEHEXPLOSION;
  +ROCKETTRAIL;
  DeathSound "weapons/rocklx";
  Obituary "$OB_MPROCKET"; // "%o rode %k's rocket."
  }
  
	States
	{
		Spawn:
			MISL A 1 Bright;
			loop;
		Death:
			MISL B 8 Bright A_Explode();
			MISL C 6 Bright;
			MISL D 4 Bright;
			stop;
	}
}


//===============================================================
// plasma stuff
//-------------------------------------------
class plastrail : Actor
{
Default
  {
  Radius 13;
  Height 8;
  +FORCEXYBILLBOARD;
  Projectile;
  RenderStyle "Add";
  Alpha 0.65;
  Damage 1;
  //DamageType "casing";
  +CORPSE;
  +DOOMBOUNCE;
  }
	
	States
	{
		Spawn:
			TNT1 A 0;
			TNT1 A 0 A_Jump(256, "a1", "a2");
		a1:
			PLSS A 0;
			goto Freeze;
		a2:
			PLSS B 0;
			goto Freeze;
			
		Freeze:
			"####" "#" 6 A_FadeOut(0.10, FTF_REMOVE);
			loop;
	}
}