//note to self: add new sfx ;]
class sg : Gun
{
Default
  {
  Decal "bhole";
  Weapon.AmmoType "PShell";
  Weapon.AmmoUse 1;
  Weapon.AmmoGive 8;
  Weapon.UpSound "weapons/sequip";
  Inventory.PickupMessage "Got the Shotgun.";
  Obituary "%o ate a faceful of lead from %k's Shotgun.";
  +WEAPON.NOAUTOFIRE;
  }
  
	States
	{
		Ready:
			SHTG A 1 A_WeaponReady();
			loop;
		
		Select:
			SHTG A 1 A_Raise(10);
			loop;
			
		DeSelect:
			SHTG A 1 A_Lower(10);
			loop;
			
		Fire:
			SHTG A 0 A_JumpIfInventory("PShell", 1, "Fire1");
		NFire:
			SHTG A 1 A_PlaySound("weapons/na", CHAN_WEAPON);
			goto Ready;
		Fire1:
			SHTG A 0 A_FireBullets (5.6, 0, 9, 6, "BulletPuff");
			"####" AAAAAA 0 A_FireProjectile("bullettracer", 0, false);
			"####" "#" 0 A_Quake(5, 2, 0, 64, "");
			"####" "#" 0 A_PlaySound("weapons/sfire", CHAN_WEAPON, 0.5);
			"####" "#" 0 A_GunFlash();
			"####" "#" 1 Offset(0,55);
			"####" "#" 1 Offset(0,50);
			"####" "#" 1 Offset(0,45);
			"####" "#" 1 Offset(0,40);
			"####" "#" 1 Offset(0,35);
			"####" BC 3;
			"####" D 0 A_PlaySound("weapons/spump", CHAN_5, 0.5);
			"####" D 6 Offset(0,45);
			"####" C 5 Offset(0,55);
			"####" B 3; 
			goto Ready;
			
		AltFire:
			TNT1 A 0 A_Quake(2, 2, 0, 32, "");
			"####" "#" 0 A_ThrowGun("flyings", "PShell");
			PUNG A 1 Offset(20,55);
			"####" "#" 1 Offset(10,45);
			"####" "#" 1 Offset(0,30);
			"####" "#" 1 Offset(-5,40);
			"####" "#" 1 Offset(-10,50);
			TNT1 A 0 A_NoGun("sg");
			stop;
			
		Flash:
			SHTF A 1 Bright A_Light(5);
			SHTF B 1 Bright A_Light(10);
			goto LightDone;
			
		Spawn:
			SHOT A -1;
			stop;
	}
}