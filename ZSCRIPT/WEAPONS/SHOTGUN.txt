//note to self: add new sfx ;]
class sg : Gun
{
Default
  {
  Decal "bhole";
  Weapon.AmmoType "PShell";
  Weapon.AmmoUse 1;
  Weapon.AmmoGive 8;
  Weapon.UpSound "weapons/sequip";
  Inventory.Icon "SHOTA0";
  Inventory.PickupMessage "Got the Shotgun.";
  Obituary "%o ate a faceful of lead from %k's Shotgun.";
  +WEAPON.NOAUTOFIRE;
  +NOEXTREMEDEATH;
  Gun.Pickup "PShotgun";
  Gun.Throw "flyings";
  Gun.Name "sg";
  }
  
	States
	{
		Ready:
			SHTG A 1 A_WeaponReady();
			loop;
		
		Select:
			SHTG A 0 Offset(-20, 50);
		Select1:
			SHTG A 1 A_Raise(10);
			loop;
			
		DeSelect:
			SHTG A 1 A_Lower(10);
			loop;
			
		Fire:
			SHTG A 0 A_JumpIfInventory("PShell", 1, "Fire1");
		NFire:
			SHTG A 0 A_PlaySound("weapons/na", CHAN_WEAPON);
			SHTG A 1 Offset(0, 40) A_AllowThrow(true);
			SHTG A 1 Offset(0, 35) A_AllowThrow(true);
			goto Ready;
		Fire1:
			SHTG A 0 A_FireBullets (5.6, 0, 7, 9, "BulletPuff", 1, 0, "bullettracer", 4);
			"####" "#" 0 A_Quake(5, 2, 0, 64, "");
			"####" "#" 0 A_PlaySound("weapons/sfire", CHAN_WEAPON, 0.5);
			"####" "#" 0 A_GunFlash();
			"####" "#" 0 A_SetPitch(pitch - 1);
			"####" "#" 1 Offset(0, 55) A_AllowThrow();
			"####" "#" 1 Offset(0, 50) A_AllowThrow();
			"####" "#" 1 Offset(0, 45) A_AllowThrow();
			"####" "#" 1 Offset(0, 40) A_AllowThrow();
			"####" "#" 1 Offset(0, 35) A_AllowThrow();
			"####" BC 3 A_AllowThrow();
			"####" D 0 A_PlaySound("weapons/spump", CHAN_5, 0.5);
			"####" D 6 Offset(0, 45) A_AllowThrow();
			"####" C 5 Offset(0, 55) A_AllowThrow();
			"####" B 3 A_AllowThrow();
			goto Ready;
			
		Flash:
			SHTF A 1 Bright A_Light(5);
			SHTF B 1 Bright A_Light(10);
			goto LightDone;
	}
}